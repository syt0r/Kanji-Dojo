plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-parcelize'
}

project.gradle.taskGraph.whenReady { graph ->
    project.tasks.findAll().forEach { task ->
        var isFdroid = task.name.containsIgnoreCase("fdroid")
        var isGoogleTask = task.name.containsIgnoreCase("GoogleServices") || task.name.containsIgnoreCase("Crashlytics")
        var isArtProfileTask = task.name.containsIgnoreCase("ArtProfile")
        if (isFdroid && (isGoogleTask || isArtProfileTask)) {
            println("Disabling f-droid task: ${task.name}")
            task.enabled = false
        }
    }
}

android {

    compileSdkVersion 33

    defaultConfig {
        applicationId "ua.syt0r.kanji"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 11
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".dev"
        }

        release {
            signingConfig debug.signingConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }

    flavorDimensions "version"

    productFlavors {

        googlePlay {
            dimension "version"
        }

        fdroid {
            dimension "version"
            applicationIdSuffix ".fdroid"
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.1'
    }
    namespace 'ua.syt0r.kanji'

}

dependencies {

    implementation project(':common')

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'

    implementation "androidx.compose.ui:ui:1.3.3"
    implementation "androidx.activity:activity-compose:1.6.1"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.ui:ui-tooling:1.3.3"
    implementation "androidx.compose.runtime:runtime-livedata:1.3.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"

    implementation "androidx.navigation:navigation-compose:2.5.3"

    implementation "androidx.compose.material3:material3:1.1.0-alpha04"

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Room
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.44.2'
    kapt 'com.google.dagger:hilt-compiler:2.44.2'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Google Play
    googlePlayImplementation platform('com.google.firebase:firebase-bom:28.0.1')
    googlePlayImplementation "com.google.firebase:firebase-analytics-ktx"
    googlePlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    googlePlayImplementation 'com.google.android.play:review-ktx:2.0.0'

    // Fdroid
    fdroidImplementation 'com.github.matomo-org:matomo-sdk-android:4.1.4'

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.3"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

}
